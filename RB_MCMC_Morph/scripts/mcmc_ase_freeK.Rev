################################################################################
#
# RevBayes Example: Inferring ancestral states and rates of morphological evolution
# 
# This file: Runs the full MCMC on an irreversible model.
#
# authors:  Sebastian HÃ¶hna, Will Freyman, April M. Wright, Michael J. Landis
#
################################################################################

#######################
# Reading in the Data #
#######################

# Import the morphological character matrix #
# this file contains only the taxa for which morphological characters are available #
morpho <- readDiscreteCharacterData("data/mammals_thinned_placenta_type.nex")

## helpers
mvi = 1
mni = 1



##############
# Tree model #
##############

# Here we use a fixed tree topology of mammals on a family level sampling
# Note that the readTrees function always returns a vector of trees, 
# so we simple take the first tree
phylogeny <- readTrees("data/mammals_thinned.tree")[1]


#########################
# Set up the rate model #
#########################

# we assume every rate i <-> j to be independent and exponentially distributed
rate_pr := phylogeny.treeLength() / 10
rate_12 ~ dnExp(rate_pr)
rate_13 ~ dnExp(rate_pr)
rate_21 ~ dnExp(rate_pr)
rate_23 ~ dnExp(rate_pr)
rate_31 ~ dnExp(rate_pr)
rate_32 ~ dnExp(rate_pr)

moves[mvi++] = mvScale( rate_12, weight=2 )
moves[mvi++] = mvScale( rate_13, weight=2 )
moves[mvi++] = mvScale( rate_21, weight=2 )
moves[mvi++] = mvScale( rate_23, weight=2 )
moves[mvi++] = mvScale( rate_31, weight=2 )
moves[mvi++] = mvScale( rate_32, weight=2 )


##########################
# Set up the rate matrix #
##########################

Q_morpho := fnFreeK( [ rate_12, rate_13, rate_21, rate_23, rate_31, rate_32 ], rescale=false )



#####################################
# Set up the root state frequencies #
#####################################

rf_prior <- [1,1,1]
rf ~ dnDirichlet( rf_prior )
moves[mvi++] = mvBetaSimplex( rf, weight=2 )
moves[mvi++] = mvDirichletSimplex( rf, weight=2 )



###################
# Set up the CTMC #
###################

# Combine all of our elements into a CTMC.

phyMorpho ~ dnPhyloCTMC(tree=phylogeny, Q=Q_morpho, rootFrequencies=rf, type="Standard")
phyMorpho.clamp(morpho)



########
# MCMC #
########

# initialize the model object #
mymodel = model(phylogeny)
# Create a vector of monitors #
# 1. for the full model #
monitors[mni++] = mnModel(filename="output/freeK.log", printgen=1)
# 2. and a few select parameters to be printed to the screen #
monitors[mni++] = mnScreen(printgen=100)
# 3. add an ancestral state monitor
monitors[mni++] = mnJointConditionalAncestralState(tree=phylogeny,
                                                   ctmc=phyMorpho,
                                                   filename="output/freeK.states.txt",
                                                   type="Standard",
                                                   printgen=1,
                                                   withTips=true,
                                                   withStartStates=false)

# Initialize the MCMC object #
mymcmc = mcmc(mymodel, monitors, moves, nruns=2, combine="mixed")

# Run the MCMC #
mymcmc.run(generations=5000, tuningInterval=200)

# check the performance of the MCMC/moves
mymcmc.operatorSummary()


# Read in the tree trace and construct the ancestral states (ASE) #
anc_states = readAncestralStateTrace("output/freeK.states.txt")
anc_tree = ancestralStateTree(tree=phylogeny, ancestral_state_trace_vector=anc_states, include_start_states=false, file="output/ase_freeK.tree", burnin=0.25, summary_statistic="MAP", site=1)

# Quit RevBayes #
q()
